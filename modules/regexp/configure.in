dnl AUTOCONF configuration for regex
dnl Bruno Haible 18.4.1995
dnl Sam Steingold 2003-2007

AC_PREREQ(2.57)
AC_INIT(regexp, 1.0, clisp-list)
AC_CONFIG_SRCDIR(regexp.lisp)
AC_CONFIG_AUX_DIR(../../src/build-aux)
AC_CONFIG_HEADERS(config.h)

RSE_BOLD
BOLD_MSG([Regexp (Common)])
CL_MODULE_COMMON_CHECKS
AC_CHECK_HEADERS(string.h sys/types.h)
AM_GNU_GETTEXT([external])
gl_C_RESTRICT

dnl called from top-level configure.in:gl_INIT [gl_FUNC_ALLOCA gl_REGEX]
dnl ac_use_included_regex comes from ../config.cache

# can we use the system-wide regex implementation?
if test "$ac_use_included_regex" = no; then
  REGEX_O=""
  MALLOC_O=""
else
  REGEX_O="regex.$ac_cv_objext"
  # must link gnulib regex.h into the build directory
  $as_ln_s $srcdir/../../src/gllib/regex.h .
  if test $gl_cv_func_malloc_posix = no; then
    MALLOC_O="malloc.$ac_cv_objext"
  fi
fi
AC_SUBST(REGEX_O)
AC_SUBST(MALLOC_O)
BOLD_MSG([Regexp (Output)])
AC_CONFIG_FILES(Makefile link.sh)
AC_OUTPUT

BOLD_MSG([Regexp (Done)])
