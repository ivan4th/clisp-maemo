dnl AUTOCONF configuration for dbus
dnl Copyright (C) 2008 Sam Steingold <sds@gnu.org>
dnl GNU GPL2+

AC_PREREQ(2.57)
AC_INIT(dbus, 1.0, clisp-list)
AC_CONFIG_SRCDIR(dbus.lisp)
AC_CONFIG_HEADERS(config.h)

RSE_BOLD
BOLD_MSG([Dbus (Common)])
if test $cl_cv_have_ffcall = no; then
  AC_MSG_ERROR([module Dbus requires FFI])
fi
CL_MODULE_COMMON_CHECKS

dnl Search for libdbus and define LIBDBUS, LTLIBDBUS and INCDBUS.
AC_CONFIG_AUX_DIR(../../src/build-aux)
AC_LIB_LINKFLAGS([dbus])

BOLD_MSG([Dbus (Headers)])
AC_CHECK_HEADERS(dbus.h dbus/dbus.h)
if test "$ac_cv_header_dbus_h" = "no" -a "$ac_cv_header_dbus_dbus_h" = "no" ;
then AC_MSG_ERROR([cannot find DBUS headers])
fi

BOLD_MSG([Dbus (Functions)])
AC_LIB_APPENDTOVAR([LIBS], [$LIBDBUS])
AC_SEARCH_LIBS(dbus_shutdown, dbus)
if test "$ac_cv_search_dbus_shutdown" = "no"; then
  AC_MSG_ERROR([cannot find DBUS library])
fi

BOLD_MSG([Dbus (Output)])
AC_CONFIG_FILES(Makefile link.sh)
AC_OUTPUT

BOLD_MSG([Dbus (Done)])
