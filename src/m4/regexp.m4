dnl -*- Autoconf -*-
dnl Copyright (C) 2004 Sam Steingold
dnl This file is free software, distributed under the terms of the GNU
dnl General Public License.  As a special exception to the GNU General
dnl Public License, this file may be distributed as part of a program
dnl that contains a configuration script generated by Autoconf, under
dnl the same distribution terms as the rest of that program.

AC_PREREQ(2.57)

AC_DEFUN([CL_REGEXP],[
AC_CHECK_HEADERS(regex.h)
if test "$ac_cv_header_regex_h" = yes; then
AC_CHECK_TYPES(regoff_t,,,[#include <regex.h>])
if test "$ac_cv_type_regoff_t" = yes; then
AC_CHECK_FUNCS(regcomp regexec regfree regerror)
if test "$ac_cv_func_regcomp" = yes -a "$ac_cv_func_regexec"   = yes -a \
        "$ac_cv_func_regfree" = yes -a "$ac_cv_func_regerror"  = yes ; then
AC_CACHE_CHECK([for a working regexp implementation],[cl_cv_regexp],
AC_RUN_IFELSE([[AC_LANG_PROGRAM([[#include <regex.h>
#include <stdio.h>
int check_re (char* pat, char* str, int count, regoff_t* beg, regoff_t* end) {
  regex_t re;
  regmatch_t *matches;
  int num, ii, status = regcomp(&re,pat,0);
  if (status) {
    fprintf(stderr,"regcomp(%s): %d\n",pat,status);
    return 1;
  }
  num = re.re_nsub+1;
  if (num != count) {
    fprintf(stderr,"regcomp(%s): %d != %d\n",pat,num,count);
    return 1;
  }
  matches = (regmatch_t*)malloc(num*sizeof(regmatch_t));
  status = regexec(&re,str,num,matches,0);
  regfree(&re);
  if (status) {
    fprintf(stderr,"regexec(%s): %d\n",str,status);
    return 1;
  }
  for (ii=0; ii < num; ii++)
    if ((beg[ii] != matches[ii].rm_so) || (end[ii] != matches[ii].rm_eo)) {
      /* regoff_t may be 64-bit! */
      fprintf(stderr,"matches(%d): [%d/%d] != [%d/%d]\n",ii,
              (int)matches[ii].rm_so,(int)matches[ii].rm_eo,
              (int)beg[ii],(int)end[ii]);
      free(matches); return 1;
    }
  free(matches);
  return 0;
}
]],[[
  regoff_t beg[3] = { 4, 0, 0 };
  regoff_t end[3] = { 9, 0, 0 };
  if (check_re("quick","The quick brown fox jumped quickly.",1,beg,end))
    return 1;
]])]],cl_cv_regexp=yes,cl_cv_regexp=no,cl_cv_regexp="guessing no"))
if test "$cl_cv_regexp" = yes; then
AC_DEFINE([HAVE_WORKING_REGEXP],1,[Define if the OS regexp works properly.])
fi
fi
fi
fi])
