dnl  -*- Autoconf -*- configuration for CLISP
dnl  Bruno Haible 2001-02-04
dnl  Sam Steingold 2002
dnl
dnl  configure.in ---<autoconf>--->> configure
dnl
dnl  configure    ---<sh>--->> config.status
dnl                      --->> unixconf.h
dnl          intparam.c  --->> intparam.h
dnl          makemake.in --->> makemake
dnl
dnl  makemake     ---<sh>--->> Makefile
dnl
dnl  Makefile     ---<make>--->> lisp.run, lispinit.mem
dnl
dnl
AC_INIT(lispbibl.d)
AC_CONFIG_HEADER(unixconf.h)
dnl
dnl           checks for programs
dnl
dnl sets variable CC
AC_PROG_CC
dnl sets variable CPP
AC_PROG_CPP
dnl
dnl           checks for UNIX variants that set DEFS
dnl
dnl DEFS _ALL_SOURCE
AC_AIX
dnl sets variables CC_GCC, GCC_X_NONE
CL_CC_GCC
dnl sets variable CC_CPLUSPLUS
CL_CC_CPLUSPLUS
dnl sets variable CC_NEED_CCPAUX
CL_CC_NEED_CCPAUX
dnl sets variable CC_NEED_DEEMA
CL_CC_NEED_DEEMA
dnl sets variable AS_UNDERSCORE, DEFS ASM_UNDERSCORE
CL_AS_UNDERSCORE
dnl
dnl           checks for programs
dnl
dnl sets variable RANLIB
CL_PROG_RANLIB
dnl sets variables INSTALL, INSTALL_DATA, INSTALL_PROGRAM
CL_PROG_INSTALL
dnl sets variable CP
CL_PROG_CP
dnl sets variable LN_S
CL_PROG_LN_S
dnl sets variable HLN
CL_PROG_HLN
dnl sets variable GROFF
AC_CHECK_PROG(GROFF,groff,groff)
dnl
dnl           checks for UNIX variants that set DEFS
dnl
dnl LIBS -lsun
CL_IRIX_SUN
dnl LIBS -lseq, -lsocket
CL_DYNIX_SEQ
dnl LIBS -lsocket
CL_SOCKET
dnl final CC check
CL_CC_WORKS
dnl
dnl           check for host type
dnl
CL_CANONICAL_HOST(autoconf)
dnl sets variables host, host_cpu, host_vendor, host_os
CL_CANONICAL_HOST_CPU
dnl
dnl           check for add-ons
dnl
dnl DEFS HAVE_ICONV, ICONV_CONST, set variable LIBICONV
AM_ICONV
dnl USE_NLS CPPFLAGS LIBINTL
dnl Set of all available languages [this is also a /bin/sh/ command!]
ALL_LINGUAS="en de fr es nl"
AM_GNU_GETTEXT([external], [need-ngettext])
dnl
dnl           checks for compiler characteristics
dnl
dnl DEFS inline
AC_C_INLINE
dnl DEFS void
CL_VOID
dnl DEFS HAVE_LONGLONG
CL_LONGLONG
dnl
dnl           checks for header files
dnl
dnl DEFS STDC_HEADERS if exist stdlib.h, stdarg.h, ...
CL_STDC_HEADERS
dnl DEFS HAVE_OFFSETOF if offsetof in stddef.h
CL_OFFSETOF
dnl DEFS HAVE_STDBOOL_H, sets variables STDBOOL_H, HAVE__BOOL
gt_STDBOOL_H
dnl DEFS HAVE_STDINT_H, sets variables STDINT_H, HAVE_LONG_64BIT, HAVE_LONGLONG_64BIT
gt_STDINT_H
dnl DEFS HAVE_UNISTD_H if exist unistd.h
CL_UNISTD_H
dnl DEFS ACCESS_NEEDS_SYS_FILE_H if sys/file.h is needed for the access() flags
CL_ACCESSFLAGS
dnl DEFS OPEN_NEEDS_SYS_FILE_H if sys/file.h is needed for the open() flags
CL_OPENFLAGS
dnl directory reading functions
CL_DIR_HEADER
dnl DEFS HAVE_SYS_UTSNAME_H
CL_UTSNAME
dnl DEFS HAVE_NETDB_H if exist netdb.h
CL_NETDB
dnl DEFS HAVE_SYS_IPC_H, HAVE_SYS_SHM_H
CL_SHM_H
dnl DEFS HAVE_TERMIOS_H, HAVE_TERMIO_H, ..., HAVE_SGTTY_H
CL_TERM
dnl sets variables X_INCLUDES, X_LIBS, DEFS HAVE_X11
CL_FIND_X
dnl
dnl           checks for typedefs
dnl
dnl DEFS CADDR_T
CL_CADDR_T
dnl DEFS SOCKLEN_T
CL_SOCKLEN_T
dnl
dnl           checks for structure members
dnl
dnl DEFS DIRENT_WITHOUT_NAMLEN
CL_DIRENT_WITHOUT_NAMLEN
dnl DEFS TM_IN_SYS_TIME
CL_STRUCT_TM
dnl
dnl           checks for functions and declarations
dnl
dnl DEFS RETSTRLENTYPE, STRLEN_CONST
CL_STRLEN
dnl DEFS RETMEMSETTYPE
CL_MEMSET
dnl set variable GMALLOC
CL_GMALLOC
dnl DEFS RETMALLOCTYPE, MALLOC_SIZE_T
CL_MALLOC
dnl DEFS RETFREETYPE
CL_FREE
dnl set variable ALLOCA, DEFS NO_ALLOCA
CL_ALLOCA
dnl DEFS HAVE__JMP, LONGJMP_RETURNS
CL_SETJMP
dnl DEFS RETSIGTYPE, SIGTYPE_DOTS
CL_TYPE_SIGNAL
dnl DEFS SIGNALBLOCK_POSIX, SIGNALBLOCK_SYSV, SIGNALBLOCK_BSD
CL_SIGNALBLOCK
dnl DEFS SIGPROCMASK_CONST
CL_SIGPROCMASK
dnl DEFS SIGNAL_NEED_REINSTALL
CL_SIGNAL_REINSTALL
dnl DEFS SIGNAL_NEED_UNBLOCK
CL_SIGNAL_UNBLOCK
dnl DEFS SIGNAL_NEED_UNBLOCK_OTHERS
CL_SIGNAL_BLOCK_OTHERS
dnl DEFS HAVE_SIGACTION
CL_SIGACTION
dnl DEFS SIGACTION_NEED_REINSTALL
CL_SIGACTION_REINSTALL
dnl DEFS SIGACTION_NEED_UNBLOCK
CL_SIGACTION_UNBLOCK
dnl DEFS HAVE_SIGINTERRUPT, HAVE_SIGVEC
CL_SIGINTERRUPT
dnl DEFS HAVE_FPU_CONTROL_T, HAVE_SETFPUCW
CL_FPU_CONTROL
dnl DEFS HAVE_RAISE
CL_RAISE
dnl DEFS RETABORTTYPE, ABORT_VOLATILE
CL_ABORT
dnl DEFS HAVE_PERROR_DECL
CL_PERROR
AC_CHECK_FUNCS(strerror)dnl ==> HAVE_STRERROR
dnl DEFS SYS_ERRLIST_CONST
CL_SYS_ERRLIST
dnl DEFS GETENV_CONST
CL_GETENV
dnl DEFS HAVE_PUTENV, PUTENV_CONST, HAVE_SETENV
CL_PUTENV
dnl DEFS SETLOCALE_CONST
CL_SETLOCALE
dnl DEFS HAVE_LC_MESSAGES
AM_LC_MESSAGES
dnl DEFS HAVE_SETRLIMIT, RLIMIT_RESOURCE_T, SETRLIMIT_CONST
CL_RLIMIT
dnl DEFS HAVE_VFORK_H, vfork
AC_FUNC_VFORK
dnl DEFS RETVFORKTYPE
CL_VFORK
AC_CHECK_FUNCS(setsid setpgid)dnl ==> HAVE_SETSID, HAVE_SETPGID
dnl DEFS EXECV_CONST, EXECV1_CONST, EXECV2_CONST
CL_EXECV
dnl DEFS EXECL_DOTS, EXECL_CONST
CL_EXECL
dnl DEFS PID_T
CL_WAITPID
dnl DEFS HAVE_SYS_RESOURCE_H, HAVE_GETRUSAGE, RUSAGE_WHO_T, HAVE_SYS_TIMES_H
CL_RUSAGE
dnl DEFS HAVE_GETCWD, GETCWD_SIZE_T
CL_GETCWD
dnl DEFS CHDIR_CONST
CL_CHDIR
dnl DEFS MKDIR_CONST
CL_MKDIR
dnl DEFS RMDIR_CONST
CL_RMDIR
dnl DEFS STAT_MACROS_BROKEN
AC_HEADER_STAT
dnl DEFS FSTAT_INLINE
CL_FSTAT
dnl DEFS STAT_CONST, STAT_INLINE
CL_STAT
dnl DEFS HAVE_LSTAT, LSTAT_CONST, LSTAT_INLINE
CL_LSTAT
dnl DEFS HAVE_READLINK, RETREADLINKTYPE, READLINK_CONST,
dnl READLINK_BUF_T, READLINK_SIZE_T
CL_READLINK
dnl DEFS ELOOP_VALUE
CL_ELOOP
dnl DEFS OPENDIR_CONST
CL_OPENDIR
dnl DEFS RETCLOSEDIRTYPE, VOID_CLOSEDIR
CL_CLOSEDIR
dnl DEFS OPEN_CONST, OPEN_DOTS
CL_OPEN
dnl DEFS RETRWTYPE, RW_BUF_T, RW_SIZE_T
CL_READ_WRITE
dnl DEFS WRITE_CONST
CL_WRITE
dnl DEFS RENAME_CONST
CL_RENAME
dnl DEFS UNLINK_CONST
CL_UNLINK
AC_CHECK_FUNCS(fsync flock)dnl ==> HAVE_FSYNC HAVE_FLOCK
AC_CHECK_HEADERS(sys/file.h)dnl ==> HAVE_SYS_FILE_H
AC_CHECK_FUNCS(gethostent shutdown)dnl ==> HAVE_GETHOSTENT HAVE_SHUTDOWN
dnl DEFS IOCTL_REQUEST_T, IOCTL_ARGUMENT_T, IOCTL_DOTS, HAVE_FIONREAD,
dnl NEED_SYS_FILIO_H, NEED_SYS_IOCTL_H, HAVE_RELIABLE_FIONREAD
CL_IOCTL
dnl DEFS FCNTL_DOTS
CL_FCNTL
dnl DEFS HAVE_SELECT, SELECT_WIDTH_T, SELECT_SET_T, SELECT_CONST, HAVE_POLL
CL_SELECT
AC_CHECK_FUNCS(ualarm)dnl ==> HAVE_UALARM
dnl DEFS HAVE_SETITIMER, SETITIMER_CONST
CL_SETITIMER
AC_CHECK_FUNCS(usleep)dnl ==> HAVE_USLEEP
dnl DEFS LOCALTIME_CONST
CL_LOCALTIME
dnl DEFS HAVE_GETTIMEOFDAY, GETTIMEOFDAY_DOTS, GETTIMEOFDAY_TZP_T
CL_GETTIMEOFDAY
dnl DEFS HAVE_FTIME
CL_FTIME
dnl DEFS HAVE_TIMES_CLOCK
CL_TIMES_CLOCK
dnl DEFS GETPWNAM_CONST
CL_GETPWNAM
dnl DEFS GETPWUID_UID_T
CL_GETPWUID
dnl DEFS HAVE_GETHOSTNAME, GETHOSTNAME_SIZE_T
CL_GETHOSTNAME
dnl DEFS HAVE_GETHOSTBYNAME, GETHOSTBYNAME_CONST
CL_GETHOSTBYNAME
dnl DEFS CONNECT_NAME_T, CONNECT_CONST, CONNECT_ADDRLEN_T
CL_CONNECT
dnl DEFS HAVE_SYS_UN_H, HAVE_SOCKADDR_UN_LEN
CL_UNIXCONN
dnl DEFS HAVE_IPV4, HAVE_IPV6, IPV6_NEED_LINUX_IN6_H, HAVE_INET_PTON,
dnl HAVE_INET_NTOP, HAVE_NETINET_IN_H, HAVE_ARPA_INET_H, RET_INET_ADDR_TYPE,
dnl INET_ADDR_SUFFIX, INET_ADDR_CONST, HAVE_NETINET_TCP_H, SETSOCKOPT_CONST,
dnl SETSOCKOPT_ARG_T, SETSOCKOPT_OPTLEN_T
CL_TCPCONN
dnl DEFS CODE_ADDRESS_RANGE, MALLOC_ADDRESS_RANGE, SHLIB_ADDRESS_RANGE,
dnl STACK_ADDRESS_RANGE
CL_ADDRESS_RANGE
dnl DEFS HAVE_GETPAGESIZE, RETGETPAGESIZETYPE
CL_GETPAGESIZE
dnl DEFS HAVE_VADVISE
CL_VADVISE
dnl DEFS HAVE_MACH_VM
CL_MACH_VM
dnl DEFS HAVE_MMAP, RETMMAPTYPE, MMAP_ADDR_T, MMAP_SIZE_T, HAVE_MMAP_ANON,
dnl HAVE_MMAP_ANONYMOUS, HAVE_MMAP_DEVZERO, HAVE_MMAP_DEVZERO_SUN4_29
CL_MMAP
dnl DEFS HAVE_MUNMAP
CL_MUNMAP
dnl DEFS HAVE_MSYNC
CL_MSYNC
dnl DEFS HAVE_MPROTECT, HAVE_WORKING_MPROTECT, MPROTECT_CONST
CL_MPROTECT
dnl DEFS SHMGET_SIZE_T
CL_SHMGET
dnl DEFS RETSHMATTYPE, SHMAT_CONST
CL_SHMAT
dnl DEFS SHMDT_ADDR_T
CL_SHMDT
dnl DEFS SHMCTL_DOTS
CL_SHMCTL
dnl DEFS HAVE_SHM if shm works
CL_SHM
dnl DEFS SHM_RMID_VALID
CL_SHM_RMID
dnl DEFS HAVE_DLOPEN, DLERROR_CONST, set variable LIBDL
CL_DYNLOAD
dnl
dnl           checks for libraries
dnl
dnl set variable LIBTERMCAP
CL_TERMCAP
dnl
dnl           checks for OS services
dnl
dnl DEFS VALID_FILENAME_CHAR
CL_FILECHARSET
dnl
dnl           checks for compiler characteristics
dnl
dnl DEFS HAVE_BUILTIN_STRLEN
CL_BUILTIN_STRLEN
dnl DEFS __CHAR_UNSIGNED__ if char is unsigned
CL_CHAR_UNSIGNED
dnl builds intparam.h
CL_MACHINE([integer types and behaviour],
${srcdir}/intparam.c,intparam.h,cl_cv_file_intparam_h)
dnl
dnl gettext po files
dnl
AC_SUBST(PACKAGE)dnl
PACKAGE="clisp"
AC_OUTPUT([po/Makefile.in])
dnl
dnl           other checks for UNIX variants
dnl
AC_OUTPUT(makemake,
[echo '#!/bin/sh' > makemake.tmp
echo '# Generated automatically from makemake.in by configure.' >> makemake.tmp
cat makemake >> makemake.tmp
mv makemake.tmp makemake
chmod a+x makemake
])
